' ChatGPT example in Dinfio

import string, gui
import openai

start
    setup()
stop

function do_input()
    key = getkeycode()

    if key == keycode.return
        question = trim(input.gettext())
        if question == ""; return; endif

        if question == "clear"
            prompt = "Hello!\nHi!\n"
            textarea.settext("Welcome to ChatGPT in Dinfio!\nThe following is a conversation with an AI.\n\n")
            input.settext("")

            return
        elseif question == "zoom in" || question == "zoomin"
            font_size += 1
            textarea.setfontsize(font_size)
            input.settext("")

            return
        elseif question == "zoom out" || question == "zoomout"
            font_size -= 1
            textarea.setfontsize(font_size)
            input.settext("")

            return
        elseif question == "reset zoom"
            font_size = 12
            textarea.setfontsize(font_size)
            input.settext("")

            return
        endif

        prompt &= question & "\n"
        textarea.settext(textarea.gettext() & "You: " & question & "\n")
        textarea.setsel(length(textarea.gettext()) - 1, length(textarea.gettext()) - 1)

        input.settext("")

        openai.completion_async(prompt, callback())
        window.settitle("ChatGPT in Dinfio - Thinking...")
    endif
stop

function callback(result)
    prompt &= result & "\n"

    textarea.settext(textarea.gettext() & "AI: " & result & "\n\n")
    textarea.setsel(length(textarea.gettext()) - 1, length(textarea.gettext()) - 1)

    window.settitle("ChatGPT in Dinfio")
stop

function setup()
    openai.api_key = "Your Key"

    global prompt = "Hello!\nHi!\n"
    global font_size = 12

    global window = gui_window("ChatGPT in Dinfio", 640, 500)
    global stack = gui_stack(vertical, window)

    global textarea = gui_textarea("Welcome to ChatGPT in Dinfio!\nThe following is a conversation with an AI.\n\n", window, 0, 0, 100, 100, true)
    global input = gui_textbox("", window, 0, 0, 100, 22)

    stack.add(textarea, true, true, padding.all, -1)
    stack.add(input, false, true, padding.all, 10)

    textarea.setfont("Menlo")
    textarea.setfontsize(font_size)
    textarea.setlocked(true)

    input.setfont("Menlo")
    input.setfontsize(12)
    input.setfocus()
    input.addevent(event.keyup, do_input())

    window.show()
stop
