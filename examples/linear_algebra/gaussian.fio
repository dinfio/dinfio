' Solving the system of linear equations using Gaussian Elimination

import math, string

start
    system = [
        [2, 1, 1, 1, 1],   ' 2a +  b +  c +  d = 1
        [1, 3, 1, 1, 2],   '  a + 3b +  c +  d = 2
        [1, 1, 4, 1, 3],   '  a +  b + 4c +  d = 3
        [1, 1, 1, 5, 25]   '  a +  b +  c + 5d = 25
    ]

    solution = gauss(system)
    print()
stop

function print()
    for i, 0, size(solution) - 1
        writeln(fromascii(toascii("a") + i) & " = " & solution[i])
    endfor
stop

function gauss(a)
    n = size(a)
    result = array(n)

    for i, 0, n - 1
        max_e = abs(a[i][i])
        max_row = i

        for j, i + 1, n - 1
            if abs(a[j][i]) > max_e
                max_e = abs(a[j][i])
                max_row = j
            endif
        endfor

        for j, i, n
            [a[max_row][j], a[i][j]] = [a[i][j], a[max_row][j]]
        endfor

        for k, i + 1, n - 1
            c = -a[k][i] / a[i][i]

            for j, i, n
                if i == j
                    a[k][j] = 0
                else
                    a[k][j] += c * a[i][j]
                endif
            endfor
        endfor
    endfor

    for i, n - 1, 0, -1
        result[i] = a[i][n] / a[i][i]

        for k, i - 1, 0, -1
            a[k][n] -= a[k][i] * result[i]
        endfor
    endfor

    return result
stop